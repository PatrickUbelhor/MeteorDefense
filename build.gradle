buildscript {
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}

	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.2.7.RELEASE'
		classpath 'com.github.node-gradle:gradle-node-plugin:2.2.3'
	}
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.github.node-gradle.node'

group 'com.patrickubelhor'
version '2.0.0'

sourceCompatibility = 1.11
targetCompatibility = 1.11

repositories {
	mavenCentral()
}

dependencies {
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
	implementation group: 'org.apache.logging.log4j', name: 'log4j-core',   version: '2.13.2'

	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat'
	implementation group: 'org.apache.tomcat.embed', name: 'tomcat-embed-jasper'
	runtimeOnly group: 'com.h2database', name: 'h2', version: '1.4.200'
}

wrapper {
	gradleVersion '6.4.1'
}

task copyReact(type: Copy) {
	from 'react/build'
	into 'build/resources/main/static/.'
}

task appNpmBuild(type: NpmTask) {
	description = 'Builds production version of the webapp'
	args = ['run', 'build']
}

task appNpmInstall(type: NpmTask) {
	description = 'Installs all dependencies from package.json'
	args = ['install']
}

appNpmBuild.dependsOn appNpmInstall
copyReact.dependsOn appNpmBuild
//compileJava.dependsOn copyReact

node {
	version = '14.3.0'
	npmVersion = '6.14.5'
	download = false
	nodeModulesDir = file("${project.projectDir}/react")
}
